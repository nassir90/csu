#!/bin/zsh

trap 'üç¥ openocd' SIGINT SIGKILL

üç¥() 1>/dev/null 2>&1 jobs %?$1 && kill %?$1

# Keep on goinginto parent directory until a file characteristic to
# the root of the pico apps repository is found. This file is taken to
# be the first parameter.
find_in_parent() {
    [ -e "$1" ] && pwd && return
    cd ..
    [ `pwd` = / ] && exit 1
    find_in_parent "$@"
}

read_target_from_cmake_file() grep -Po '(?<=add_executable\()[^)]+(?=\))' `find_in_parent CMakeLists.txt`/CMakeLists.txt

declare emacs stm
>/dev/null which gdb-multiarch && gdb="gdb-multiarch" || gdb="arm-none-eabi-gdb"
gdb_port=$[ $RANDOM % 1024 + 1024 ]
pico_apps_home=${PICO_APPS_HOME:-`find_in_parent pico_sdk_import.cmake`}
target_build_dir=$pico_apps_home/build/$(realpath `pwd` --relative-to $pico_apps_home)
target=(-t `read_target_from_cmake_file`)
zparseopts -F -D -K -- \
           {p,-port}:=gdb_port \
           {e,-emacs}=emacs \
           {s,-stm}=stm \
           {t,-target}:=target \
           {b,-build-only}=build_only \
           {r,-run-only}=run_only \
    || exit 1

if [ ! -e "$pico_apps_home/build" ]
then
    # Initialise cmake direcotry if it does not exist yet
    (cd "$pico_apps_home"
     mkdir build
     cd build
     cmake ..)
fi

[ -f .gdb ] && gdb_config_option=(-x `realpath .gdb`)

# If running in emacs mdoe, redirect stdout to null to avoid breaking
# gdb plugin.
[ $emacs ] && exec 4>&1 5>&2 1>/dev/null 2>/dev/null

make_if_not_emacs() {
    if [ ! $emacs ]
    then
        echo '[ Calling Make... ]'
        make
    fi
}

if [ ! $stm ]
then
    [ -z "$target[2]" ] && echo Target not specified! && exit 1
    cd $target_build_dir
    
    make_if_not_emacs || exit 1
    [ $build_only ] && exit

    echo '[ Starting OpenOCD... ]'
    openocd \
        -f interface/cmsis-dap.cfg \
        -c "adapter speed 5000" \
        -f target/rp2040.cfg \
        -c "program $target[2].elf" \
        -s tcl &

    gdb_file=$target[2].elf
    gdb_options=(-ex "target extended-remote localhost:3333"
                 -ex "load")
else
    make_if_not_emacs || exit 1
    [ $build_only ] && exit

    echo '[Starting QEMU...]'
    qemu-system-gnuarmeclipse \
        --board STM32F4-Discovery \
        -gdb tcp::$gdb_port \
        --image build/firmware.elf &

    gdb_file=build/firmware.elf
    gdb_options=(-ex "target remote localhost:$gdb_port"
                 -ex "b End_Main")
fi

echo '[Starting GDB...]'

# Basically force the command 'continue' and 'exit' to be
# executed. Exit will prompt the user whether or not he /really/ wants
# to exit so we have to force standard in to only contain the string
# 'y'.
[ $run_only ] && gdb_options+=(-ex c -ex exit) && exec 0<<<y

# Restore stdout to give output to emacs.
[ $emacs ] && exec 1>&4 2>&5
$gdb $gdb_file \
     ${emacs:+'-i=mi'} \
     $gdb_config_option \
     $gdb_options

# Kill openocd if it still exists
üç¥ openocd
